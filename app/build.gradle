apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://github.com/linchaolong/stetho-realm/raw/master/maven-repo' }
}

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}

def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }
    return p.text.trim()
}

def gitTimestamp() {
    def p = 'git log -n 1 --format=%at'.execute([], rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }
    return p.text.trim()
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId 'org.petabytes.awesomeblogs'
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 2
        versionName "1.0"
        buildConfigField 'String', 'GIT_SHA', '\"${gitSha()}\"'
        buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    signingConfigs {
        release {
            storeFile file(String.valueOf(System.getenv("KEYSTORE")))
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix '.debug'
        }
        release {
            debuggable false
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            setProperty("archivesBaseName", "awesomeBlogs-${defaultConfig.versionName}-${defaultConfig.versionCode}")
        }
    }
    productFlavors {
        staging {
            applicationId 'org.petabytes.awesomeblogs.staging'
        }
        production {
            applicationId 'org.petabytes.awesomeblogs'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':api')
    compile project(':coordinator')
    compile project(':remark')
    compile project(':markDownView')
    compile project(':slidingMenu')
    compile 'com.android.support:design:25.1.1'
    compile 'com.google.firebase:firebase-core:10.0.1'
    compile 'com.tapadoo.android:alerter:1.0.1'
    compile 'com.flipboard:bottomsheet-core:1.5.0'
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'
    compile 'uk.co.chrisjenx:calligraphy:2.2.0'
    compile 'com.github.ybq:Android-SpinKit:1.1.0'
    compile 'com.github.castorflex.verticalviewpager:library:19.0.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.0.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
        transitive = true;
    }

    debugCompile 'com.facebook.stetho:stetho:1.4.2'
    debugCompile 'com.uphyca:stetho_realm:2.0.1'
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}
apply plugin: 'com.google.gms.google-services'