apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.jakewharton.hugo'

repositories {
    maven { url "https://jitpack.io" }
}

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'me.tatarka:gradle-retrolambda:3.4.0'
    }
}

def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }
    return p.text.trim()
}

def gitTimestamp() {
    def p = 'git log -n 1 --format=%at'.execute([], rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }
    return p.text.trim()
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "org.petabytes.awesomeblogs"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
        buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix '.debug'
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        staging {
            applicationId 'org.petabytes.awesomeblogs.staging'
        }
        production {
            applicationId 'org.petabytes.awesomeblogs'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':api')
    compile project(':coordinator')
    compile 'com.android.support:design:25.1.1'
    compile 'com.facebook.fresco:fresco:1.0.1'
    compile 'com.facebook.rebound:rebound:0.3.8'
    compile 'com.flipboard:bottomsheet-core:1.5.0'
    compile 'com.overzealous:remark:1.0.0'
    compile 'org.jsoup:jsoup:1.10.1'
    compile 'com.github.ybq:Android-SpinKit:1.1.0'
    compile 'com.github.castorflex.verticalviewpager:library:19.0.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.0.1'

    debugCompile 'com.facebook.stetho:stetho:1.4.1'
    androidTestCompile 'com.google.code.findbugs:jsr305:3.0.1'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}
